#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [session]"
  echo
  echo 'Connect to the shpool'
end

argparse --max-args=1 'h/help&' 'd/debug&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set    fzf_flags --multi
set -a fzf_flags --sort
set -a fzf_flags --prompt='session> '
if test (count $argv) -ne 0
  set -a fzf_flags --select-1
  set -a fzf_flags "--query=$(string join -- ' ' $argv)"
end

set session
if test (count $argv) -eq 0
  set session $(shpool list | tail -n +2 | string match -r '^\S+' | 9fzf $fzf_flags)
else
  set session $argv[1]
end
if test -z "$session"
  return 0
end

set user $(id -u)
set userdir /run/user/$user/shpool/sessions/$session
set values $(printenv | string match -e -r '^KITTY')

if test (count $values) -gt 0
  mkdir -p $userdir >/dev/null || true
  printf "%s\n" $values > $userdir/kitty.env
end

exec shpool attach -f $session
