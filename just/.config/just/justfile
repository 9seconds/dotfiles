# vim: set ft=just sts=2 sw=2 ts=2:

alias e := edit

# -----------------------------------------------------------------------------

# list targets
@default:
  just -g --list

# edit justfile
@edit:
  $EDITOR {{justfile()}}

# run apt upgrade
[linux]
@up-system:
  sudo apt update -q
  sudo apt full-upgrade --yes
  sudo apt autoremove --yes --purge
  command -v kitten 2>/dev/null && kitten update-self

# run brew upgrade
[macos]
@up-system: brew-upgrade
  mise self-update --yes

# update nvim packages
@up-nvim:
  nvim --headless '+Lazy! sync' +TSUpdateSync '+lua require("mason-registry").refresh()' +qa

# update zellij plugins
@up-zellij: up-zellij-zjstatus

@up-zellij-zjstatus:
  mkdir -p ~/.local/share/zellij || true
  curl -sSL --retry 5 --fail-with-body -o ~/.local/share/zellij/zjstatus.wasm https://github.com/dj95/zjstatus/releases/latest/download/zjstatus.wasm

@up-fish:
  fish -ic "fisher update"

# update mise stuff
@up-mise $RUSTUP_INIT_SKIP_PATH_CHECK="1":
  mise plugins update
  MAKEFLAGS='-j {{num_cpus()}}' mise up
  mise prune -y
  mise reshim

# update all
@up: up-system up-nvim up-mise up-zellij up-fish

# wrapper for brew install that updates bundle
[group("brew")]
[macos]
@brew-install name: && brew-update-bundle
  brew install '{{name}}'

# wrapper for brew uninstall that updates bundle
[group("brew")]
[macos]
@brew-uninstall name: && brew-update-bundle
  brew uninstall '{{name}}'
  brew autoremove

# update brew bundle
[group("brew")]
[macos]
@brew-update-bundle:
  brew bundle dump --force --casks --brews --describe --taps --mas

# upgrade brew packages
[group("brew")]
[macos]
@brew-upgrade:
  brew update
  brew upgrade --greedy-latest
  brew autoremove
  brew cleanup --prune=all --scrub
  rm -rf "$(brew --cache)"
  just -g brew-update-bundle
