# vim: set ft=just sts=2 sw=2 ts=2:

alias e := edit

# -----------------------------------------------------------------------------

# list targets
@default:
  just -g --list

# edit justfile
@edit:
  $EDITOR {{justfile()}}

# run apt upgrade
[linux]
@up-system:
  sudo apt update -q
  sudo apt full-upgrade --yes
  sudo apt autoremove --yes --purge

# run brew upgrade
[macos]
@up-system:
  brew update
  brew upgrade --greedy-latest
  brew autoremove
  brew cleanup --prune=all --scrub
  rm -rf "$(brew --cache)"

# update nvim packages
@up-nvim:
  nvim --headless '+Lazy! sync' +TSUpdateSync '+lua require("mason-registry").refresh()' +MasonToolsUpdateSync +qa

# update mise stuff
@up-mise $RUSTUP_INIT_SKIP_PATH_CHECK="1":
  MAKEFLAGS='-j {{num_cpus()}}' mise up

# update all
@up: up-system up-nvim up-mise

# wrapper for brew install that updates bundle
[macos]
@brew-install name: && brew-update-bundle
  brew install '{{name}}'

# wrapper for brew uninstall that updates bundle
[macos]
@brew-uninstall name: && brew-update-bundle
  brew uninstall '{{name}}'
  brew autoremove

# update brew bundle
[macos]
@brew-update-bundle:
  brew bundle dump --force --cask --describe --taps --mas
