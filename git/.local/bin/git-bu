#!/usr/bin/env fish
# vim: set ts=2 sts=2 sw=2:

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-b/--base] [-m/--mergebase] [-h/--help] [-d/--debug]"
  echo
  echo 'Update branch using upstream information.'
  echo
  echo ' -h / --help       Show this message'
  echo ' -d / --debug      Run in debug mode'
  echo ' -b / --base       Select base commit for merge'
  echo ' -m / --mergebase  Use merge base for base commit'
end

argparse --max-args=1 'h/help&' 'd/debug&' 'b/base&' 'm/mergebase&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set name $(git branch --show-current)
set remote $(git config get --local branch.{$name}.remote)
set merge $(git config get --local branch.{$name}.merge)
set upstream $(git config get --local branch.{$name}.upstream)

set remote_branch $remote$(string replace -- 'refs/heads' '' $merge)
if test (count $upstream) -gt 0
  set remote_branch $upstream
end

set    git_flags --empty=drop
set -a git_flags --autosquash
set -a git_flags --autostash

if set -q _flag_base
  set -a git_flags --onto $remote_branch
  set -a git_flags $(git pc)
else if set -q _flag_mergebase
  set -a git_flags --onto $remote_branch
  set -a git_flags $(git merge-base HEAD $remote_branch)
else
  set -a git_flags $remote_branch
end

exec git rebase $git_flags
