#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [name]"
  echo
  echo 'Update branch using upstream information'
end

argparse --max-args=1 'h/help&' 'd/debug&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

if test (count $argv) -eq 0
  set name $(git symbolic-ref --short HEAD)
else
  set name $argv[1]
end

set remote $(git config get --local branch.{$name}.remote)
set merge $(git config get --local branch.{$name}.merge)
set upstream $(git config get --local branch.{$name}.upstream)

set remote_branch $remote$(string replace -- 'refs/heads' '' $merge)
if test (count $upstream) -gt 0
  set remote_branch $upstream
end

git rebase --empty=drop --autosquash --autostash $remote_branch
