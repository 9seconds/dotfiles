#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [name]"
  echo
  echo 'Show information about branch'
end

argparse --max-args=1 'h/help&' 'd/debug&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

if test (count $argv) -eq 0
  set name $(git symbolic-ref --short HEAD)
else
  set name $argv[1]
end

set remote $(git config get --local branch.{$name}.remote)
set merge $(git config get --local branch.{$name}.merge)
set upstream $(git config get --local branch.{$name}.upstream)
set url $(git config get --local branch.{$name}.url)

set remote_branch $remote$(string replace -- 'refs/heads' '' $merge)

git rev-list --left-right --count $remote_branch...$name | read remote_ahead local_ahead

echo "Remote branch [local:$local_ahead remote:$remote_ahead]: $remote_branch"
echo "Upstream:" $upstream
echo "URL:" $url
