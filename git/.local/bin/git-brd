#!/usr/bin/env fish
# vim: set ts=2 sts=2 sw=2:

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [-r/--remote] [query...]"
  echo
  echo 'Remove branches.'
  echo '  query          fzf query to insert'
  echo
  echo '  -h / --help    show this help'
  echo '  -d / --debug   run in debug mode'
  echo '  -r / --remote  remove remote branches as well'
end

argparse --max-args=0 'h/help&' 'd/debug&' 'r/remote&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set    fzf_flags --multi
set -a fzf_flags --prompt='branch> '
if test (count $argv) -ne 0
  set -a fzf_flags --select-1
  set -a fzf_flags "--query=$(string join -- ' ' $argv)"
end

git for-each-ref --color=always --format='%(refname:short)  %(color:dim)%(upstream:short)' refs/heads \
  | 9fzf $fzf_flags \
  | while read -t local remote
    string replace -r '^([^/]+)/(.+)$' '$1 $2' $remote | read origin name
    git branch -D $local
    if set -q _flag_remote; and test -n "$origin"
      git push --delete $origin $name
    end
end
