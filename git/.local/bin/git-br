#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-a/-all] [-h/--help] [-d/--debug] [query...]"
  echo
  echo 'Switch to another branch.'
  echo '  query          fzf query to insert'
  echo
  echo '  -h / --help    show this help'
  echo '  -d / --debug   run in debug mode'
  echo '  -a / --all     show all branches'
end

argparse 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set pb_flags
if set -q _flag_debug
  set -a pb_flags -d
end
if set -q _flag_all
  set -a pb_flags -a
end

exec git switch $(git pb $pb_flags)
