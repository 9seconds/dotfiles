#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-a/-all] [-h/--help] [-d/--debug] [query]"
  echo
  echo 'Switch to another branch'
end

argparse 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set git_flags --color=always
if set -q _flag_all
  set -a git_flags -a
end

set    fzf_flags --prompt='branch> '
set -a fzf_flags --preview="git l --color=always -n 30 {}"

if test (count $argv) -ne 0
  set -a fzf_flags --select-1
  set -a fzf_flags --query=$(string escape -- $(string join -- ' ' $argv))
end

exec git switch $(\
  git branch $git_flags \
    | string match -gr '^\s+(\S+)$' \
    | sort \
    | 9fzf $fzf_flags \
    | string replace -r '^remotes\/.+?\/' '' \
)
