#!/usr/bin/env fish
# vim: set ts=2 sts=2 sw=2:

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [-u/--url] [-t/--test] name start-point"
  echo
  echo 'Create and setup a new branch.'
  echo '  name           stem of the branch'
  echo '  start-point    what branch to use as a start point'
  echo
  echo '  -h / --help    show this help'
  echo '  -d / --debug   run in debug mode'
  echo '  -u / --url     issue URL. Do not forget to set GIT_URL_MATCH AND GIT_URL_REPLACE.'
  echo '  -t / --test    test what it would create'
end

argparse --min-args=2 --max-args=2 'h/help&' 'd/debug&' 'u/url=&' 't/test&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set name $GIT_USERNAME

set start_point $argv[2]
if not git show-ref --verify refs/remotes/$start_point 2>/dev/null 1>&2
  echo "Incorrect start point" 2>&1
  return 1
end

if set -q GIT_URL_MATCH; and set -q _flag_url
  set replace_to '$1'
  if set -q GIT_URL_REPLACE
    set replace_to $GIT_URL_REPLACE
  end
  set -a name $(string replace -r -- $GIT_URL_MATCH $replace_to $_flag_url)
end
set -a name $argv[1]

set branch_name $(string lower -- $name)
set branch_name $(string join -- '-' $branch_name)
set branch_name $(string escape -- $branch_name)

if set -q _flag_test
  echo 'Branch name:' $branch_name
  echo 'Start point:' $start_point
  return 0
end

set config_prefix branch.$branch_name

git switch --no-track --no-guess -c $branch_name $start_point
git config set --local {$config_prefix}.remote      origin
git config set --local {$config_prefix}.merge       refs/heads/$branch_name
git config set --local {$config_prefix}.upstream    $start_point
git config set --local {$config_prefix}.startcommit $(git rev-parse HEAD)

if set -q _flag_url
  git config set --local {$config_prefix}.url $_flag_url
end
