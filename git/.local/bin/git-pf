#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [query...]"
  echo
  echo 'Pick files with fzf'
end

argparse 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set    fzf_flags --scheme=path
set -a fzf_flags --sort
set -a fzf_flags --multi
set -a fzf_flags --prompt='file> '
set -a fzf_flags --preview='bat --plain --color=always {}'

if test (count $argv) -ne 0
  set -a fzf_flags --select-1 --query=$(string collect $argv)
end

set root $(git rev-parse --show-toplevel)
git status --porcelain=1 \
  | string replace -fr '^[^D].+\s+(\S+)$' $root'/$1' \
  | path normalize \
  | 9fzf $fzf_flags
