#!/usr/bin/env fish
# vim: set ts=2 sts=2 sw=2:

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-a/-all] [-h/--help] [-d/--debug] [query...]"
  echo
  echo 'Pick branch with fzf.'
  echo '  query          fzf query to insert'
  echo
  echo '  -h / --help    show this help'
  echo '  -d / --debug   run in debug mode'
  echo '  -a / --all     show all branches'
end

argparse 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set git_flags --color=never
if set -q _flag_all
  set -a git_flags -a
end

set    fzf_flags --sort
set -a fzf_flags --prompt='branch> '
set -a fzf_flags --preview='git l --color=always -n 30 {}'

if test (count $argv) -ne 0
  set -a fzf_flags --select-1
  set -a fzf_flags "--query=$(string join -- ' ' $argv)"
end

git branch $git_flags \
  | string replace -fr -- '^\s+(?:remotes\/)?(\S+)$' '$1' \
  | 9fzf $fzf_flags
