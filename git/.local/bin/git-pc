#!/usr/bin/env fish
# vim: set ts=2 sts=2 sw=2:

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [-b/--branch branch] [-s/--short] [query...]"
  echo
  echo 'Pick commit with fzf.'
  echo '  query          fzf query to insert'
  echo
  echo '  -h / --help    show this help'
  echo '  -d / --debug   run in debug mode'
  echo '  -b / --branch  name of the branch to look into'
  echo '  -n / --records how many records to look for (default is 100)'
  echo '  -s / --short   show short commit hashes only'
end

argparse --max-args=1 'h/help&' 'd/debug&' 's/short&' 'n/records=&' 'b/branch=&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end


set git_log_cmd git l --color=always
if set -q _flag_records
  set -a git_log_cmd -n $flag_records
else
  set -a git_log_cmd -n 100
end
if set -q _flag_branch
  set -a git_log_cmd $_flag_branch
end

set    fzf_flags --scheme=history
set -a fzf_flags --prompt='commit> '
set -a fzf_flags --preview='git difftool -t difftastic {+1}~1 {+1}'
set -a fzf_flags --accept-nth 1

if test (count $argv) -ne 0
  set -a fzf_flags --select-1
  set -a fzf_flags "--query=$(string join -- ' ' $argv)"
end

set -x DFT_DISPLAY inline
set -x DFT_COLOR always

set commit $($git_log_cmd | 9fzf $fzf_flags)
if not set -q _flag_short
  exec git rev-parse $commit
end
echo $commit
