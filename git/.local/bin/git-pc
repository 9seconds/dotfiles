#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [branch]"
  echo
  echo 'Pick commit with fzf'
end

argparse --max-args=1 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set git_log_cmd git l -n 100 --color=always
if test (count $argv) -ne 0
  set -a git_log_cmd $argv[1]
end

set -x DFT_DISPLAY inline
set -x DFT_COLOR always

exec git rev-parse $(\
  $git_log_cmd \
    | 9fzf \
      --scheme=history \
      --prompt='commit> ' \
      --preview='git difftool -t difftastic {+1}~1 {+1}' --accept-nth 1 \
  )
