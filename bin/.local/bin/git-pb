#!/usr/bin/env fish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-a/-all] [-h/--help] [-d/--debug] [query]"
  echo
  echo 'Show branches with fzf'
end

argparse --max-args=1 'h/help&' 'd/debug&' 'a/all&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set git_branch_cmd git branch --color=always
if set -q _flag_all
  set -a git_branch_cmd -a
end

set fzf_cmd 9fzf --prompt='branch> ' --preview="git l --color=always -n 30 {}" --select-1
if test (count $argv) -eq 1
  set -a fzf_cmd --query=$argv[1]
end

$git_branch_cmd \
  | string match -gr '^\s+(.+)$' \
  | sort \
  | $fzf_cmd
