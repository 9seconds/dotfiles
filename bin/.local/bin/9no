#!/usr/bin/env fish
# vim: set sw=2 sts=2 ts=2

# Notify that command is done.

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] [-c/--critical] title body..."
  echo
  echo 'Notify that command is done.'
  echo
  echo 'Options:'
  echo '  -h, --help      Show this help message and exit'
  echo '  -d, --debug     Run in debug mode'
  echo '  -c, --critical  Execute with critical priority'
end

argparse --min-args=2 'h/help&' 'd/debug&' 'c/critical&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

if not type -qf kitten
  echo 'Only kitten terminal is supported' 1>&2
  return 1
end

set flags
if set -q _flag_critical
  set -a flags --urgency critical
end

exec kitten notify \
  $flags \
  $argv[1] \
  $(string join -- ' ' $(string escape -- $argv[2..]))
