#!/usr/bin/env fish
# vim: set sw=2 sts=2 ts=2

# Show lines from start to finish

set this_script_path $(path resolve $(status --current-filename))
set this_script_name $(path basename $this_script_path)

function show_help
  echo "Usage: $this_script_name [-h/--help] [-d/--debug] <start> <finish>"
  echo
  echo 'Show lines from start to finish'
  echo
  echo 'Options:'
  echo '  -h, --help    Show this help message and exit'
  echo '  -d, --debug   Run in debug mode'
end

argparse --min-args=1 --max-args=2 'h/help&' 'd/debug&' -- $argv
or begin
  show_help
  return 1
end

if set -q _flag_debug
  set fish_trace 1
end
if set -q _flag_help
  show_help
  return 0
end

set start $argv[1]
set finish $argv[-1]
set output cat

# _lines 10 9
if test $start -ge 0; and test $finish -ge 0; and test $finish -lt $start
  set output tac

# _lines -2 10
else if test $start -lt 0; and test $finish -ge 0
  set output tac
  set tmp $start
  set start $finish
  set finish $tmp

# _lines -5 -2
else if test $start -lt 0; and test $finish -lt 0; and test $start -lt $finish
  set output tac
  set tmp $start
  set start $finish
  set finish $tmp
end

set start_cmd "tail -n +$(math 1 + $start)"
if test $start -lt 0
  set start_cmd "tail -n +$(math -$start)"
end

head -n $finish | eval $start_cmd | $output
